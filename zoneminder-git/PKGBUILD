# Maintainer: Troy Will <troydwill@gmail.com>
pkgname=zoneminder
pkgver=1.28.0
pkgrel=4
pkgdesc='Capture, analyse, record and monitor video security cameras'
arch=( i686 x86_64 mips64el arm )
     
backup=( etc/zm.conf )
url="https://github.com/ZoneMinder/ZoneMinder/releases"
license=( GPL )
     
depends=(
    apache
    cambozola
    gnutls
    mariadb
    perl-archive-zip
    perl-date-manip
    perl-dbd-mysql
    perl-dbi
    perl-expect
    perl-libwww
    perl-mime-lite
    perl-mime-tools
    perl-php-serialization
    perl-net-sftp-foreign
    perl-sys-mmap
    perl-time-modules
    perl-x10
    php
    php-apache
    php-gd
    php-mcrypt
    polkit
)

makedepends=(
    netpbm
    cmake
)

optdepends=(
    netpbm
)
     
install=$pkgbase.install
     
source=(
    git://github.com/$pkgname/$pkgname.git
    httpd-zoneminder.conf
    zoneminder.service
)

# Because the source is not static, skip Git checksum:        
sha512sums=(
    'SKIP'
    'fb9bf263c37fae30d775872a33cb319f2f2a7a4f38faff8c143253dbefd7278b295d0805e11ace6423a8ec2b50ef60f3426b6e6a53548c867ef7f109baa52c36'
    '0dccb53546fb5d73dbcb9a868493f984b409b1f155ae2199c8b3e4a6f18c384a131cccec1c10d3d469d059f4d439cdddbd334a1dbcb2a787228d4359b2c8da02'
)
     
pkgver() {
    cd "$srcdir/$_gitname"
    printf "%s.r%s.%s.%s" "$pkgver" "$(git rev-list --count HEAD)" "$pkgrel" "$(git rev-parse --short HEAD)"
}

build() {
    cd $srcdir/$pkgname

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DZM_CGIDIR=/srv/http/cgi-bin \
        -DZM_WEBDIR=/srv/http/zoneminder \
        -DZM_WEB_USER=http \
        -DZM_CONTENTDIR=/var/cache/zoneminder \
        -DZM_LOGDIR=/var/log/zoneminder \
        -DZM_RUNDIR=/srv/zoneminder \
        -DZM_TMPDIR=/srv/zoneminder/tmp \
        -DZM_SOCKDIR=/srv/zoneminder/socks .
    
    make V=0
}
     
package() {

    cd $srcdir/$pkgname

    DESTDIR=$pkgdir make install

    # BEGIN CREATE_ZONEMINDER_DIRECTORIES
    mkdir -pv           $pkgdir/var/{cache/$pkgname,log/$pkgname}
    chown -Rv http.http $pkgdir/var/{cache/$pkgname,log/$pkgname}
    
    mkdir -pv          $pkgdir/srv/zoneminder/socks
    chown -v http.http $pkgdir/srv/zoneminder/socks
    
    mkdir -pv          $pkgdir/srv/zoneminder/tmp
    chown -v http.http $pkgdir/srv/zoneminder/tmp
    
    chown -v  http.http $pkgdir/etc/zm.conf 
    chmod 0700          $pkgdir/etc/zm.conf
    # END CREATE_ZONEMINDER_DIRECTORIES

    # Make content directories in /var/cache/zoneminder and to link them in /srv/http/zoneminder
    for i in events images temp; do
        mkdir $pkgdir/var/cache/$pkgname/$i
        chown -v    http.http                $pkgdir/var/cache/$pkgname/$i
        ln -s /var/cache/$pkgname/$i         $pkgdir/srv/http/$pkgname/$i
        chown -v --no-dereference http.http  $pkgdir/srv/http/$pkgname/$i
    done

    # Create a link to the Zoneminder cgi binaries
    ln -sv /srv/http/cgi-bin $pkgdir/srv/http/$pkgname

    chown -h http.http $pkgdir/srv/http/{cgi-bin,$pkgname,$pkgname/cgi-bin}

    # Link Cambozola
    ln -s /usr/share/cambozola/cambozola.jar $pkgdir/srv/http/$pkgname

    # Install configuration files
    mkdir -p $pkgdir/etc/httpd/conf/extra
    install -D -m 644 $srcdir/httpd-$pkgname.conf $pkgdir/etc/httpd/conf/extra
    mkdir -p $pkgdir/usr/lib/systemd/system
    install -D -m 644 $srcdir/$pkgname.service    $pkgdir/usr/lib/systemd/system
    install -D -m 644 COPYING                     $pkgdir/usr/share/license/$pkgname
    install -D -m 644 db/zm*.sql                  $pkgdir/usr/share/$pkgname/db     

}
