# Contributor: Troy Will                 <troydwill@gmail.com>
# Contributor: /dev/rs0                  </dev/rs0@secretco.de.com>
# Contributor: Jacek Burghardt           <jacek@hebe.us>
# Contributor: Vojtech Aschenbrenner     <v@asch.cz>
# Contributor: Jason Gardner             <buhrietoe@gmail.com>
# Contributor: Ross melin                <rdmelin@gmail.com>
# Contributor (Parabola): Márcio Silva   <coadde@lavabit.com>
# Contributor (Parabola): André Silva    <emulatorman@lavabit.com>
# Orginally based on a Debian Squeeze package
     
pkgbase=zoneminder
pkgname=zoneminder
pkgver=1.28.0
pkgrel=1
pkgdesc='Capture, analyse, record and monitor video security cameras'
arch=( i686 x86_64 mips64el arm )
     
backup=( etc/zm.conf )
url="https://github.com/ZoneMinder/ZoneMinder/releases"
license=( GPL )
     
depends=(
    apache
    cambozola
    gnutls
    mariadb
    perl-archive-zip
    perl-date-manip
    perl-dbd-mysql
    perl-dbi
    perl-expect
    perl-libwww
    perl-mime-lite
    perl-mime-tools
    perl-php-serialization
    perl-net-sftp-foreign
    perl-sys-mmap
    perl-time-modules
    perl-x10
    php
    php-apache
    php-gd
    php-mcrypt
)

makedepends=(
    netpbm
    cmake
)

optdepends=(
    netpbm
)
     
install=$pkgbase.install
     
source=(
    https://github.com/ZoneMinder/ZoneMinder/archive/v$pkgver.tar.gz
    httpd-zoneminder.conf
    zoneminder.service
)

sha512sums=('ac2a036adac436d76db8bb0b161d063111cc7cc7f6bcb9ca1b46862cfb0285b291926df716df645c0be47b7465bbbf3b1050c85529ff5068bdcc34f1c8ba6df1'
            'fb9bf263c37fae30d775872a33cb319f2f2a7a4f38faff8c143253dbefd7278b295d0805e11ace6423a8ec2b50ef60f3426b6e6a53548c867ef7f109baa52c36'
            '0dccb53546fb5d73dbcb9a868493f984b409b1f155ae2199c8b3e4a6f18c384a131cccec1c10d3d469d059f4d439cdddbd334a1dbcb2a787228d4359b2c8da02')
     
build() {
    cd $srcdir/ZoneMinder-$pkgver

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DZM_CGIDIR=/srv/http/cgi-bin \
        -DZM_WEBDIR=/srv/http/zoneminder \
        -DZM_WEB_USER=http \
        -DZM_CONTENTDIR=/var/cache/zoneminder \
        -DZM_LOGDIR=/var/log/zoneminder \
        -DZM_RUNDIR=/var/run/zm \
        -DZM_TMPDIR=/var/tmp \
        -DZM_SOCKDIR=/srv/zoneminder/socks/ .
    
    make V=0
}
     
package() {

    cd $srcdir/ZoneMinder-$pkgver

    DESTDIR=$pkgdir make install

    # BEGIN CREATE_ZONEMINDER_DIRECTORIES
    mkdir -p $pkgdir/var/run/zm
    chown -v http.http $pkgdir/var/run/zm
    mkdir -p $pkgdir/var/{cache/$pkgbase,log/$pkgbase}
    mkdir -p $pkgdir/srv/zoneminder/socks
    chown -Rv http.http $pkgdir/{etc/zm.conf,var/{cache/$pkgbase,log/$pkgbase}}
    chown -Rv http.http $pkgdir/srv/zoneminder/socks
    chmod 0700 $pkgdir/etc/zm.conf
    # END CREATE_ZONEMINDER_DIRECTORIES

    # Make content directories in /var/cache/zoneminder and link them
    for i in events images temp; do
        mkdir $pkgdir/var/cache/$pkgbase/$i
        # chown -v    http.http                $pkgdir/var/cache/$pkgbase/$i
        ln -s /var/cache/$pkgbase/$i         $pkgdir/srv/http/$pkgbase/$i
        chown -v --no-dereference http.http  $pkgdir/srv/http/$pkgbase/$i
    done

    # Create a link to the Zoneminder cgi binaries
    ln -sv /srv/http/cgi-bin $pkgdir/srv/http/$pkgbase

    chown -h http.http $pkgdir/srv/http/{cgi-bin,$pkgbase,$pkgbase/cgi-bin}

    # Link Cambozola
    ln -s /usr/share/cambozola/cambozola.jar $pkgdir/srv/http/$pkgbase

    # Install configuration files
    mkdir -p $pkgdir/etc/httpd/conf/extra
    install -D -m 644 $srcdir/httpd-$pkgbase.conf $pkgdir/etc/httpd/conf/extra
    mkdir -p $pkgdir/usr/lib/systemd/system
    install -D -m 644 $srcdir/$pkgbase.service    $pkgdir/usr/lib/systemd/system
    install -D -m 644 COPYING                     $pkgdir/usr/share/license/$pkgbase
    install -D -m 644 db/zm*.sql                  $pkgdir/usr/share/$pkgbase/db     

}
